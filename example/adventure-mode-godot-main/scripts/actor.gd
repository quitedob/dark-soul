extends CharacterBody3D

class_name Actor

static var ALL_EVER_MADE : Array[Actor] = []

@export var character : Character

var desired_move = Vector3.ZERO
var transformed_move_dir = Vector2.ZERO
var desired_turn = 0.0 # left or right -+ 
var lock_targ_pos : Vector3 = Vector3.ZERO
# NOTE - Desired move used to handle everything. But locking on and strafing 
# 		 involved having the direction you were looking, IE turned towards
#		 and the direction of movement, be separate. 

@export_group("Animation Flags")
@export var block = false
@export var parry = false
@export var dodge = false
@export var sprint = false
@export var invulnerable = false
@export var prop_L_hurtbox = false
@export var prop_R_hurtbox = false



@onready var animation_tree : AnimationTree = $AnimationTree 
# SECTION Animation hacking system
# animation set NAME
# These hold nodes that need to be updated with various values
# See apply_animation_params to behold the shame
var aset_BLOCK = []
var aset_MOVE = []
var aset_TURN = []
var aset_JUMP = []
# !SECTION

@export_group("Movement Packages")
# 0th index is default and should always be possible to move too.
@export var movement_sets : Array[MovementPackage] # NOTE - walk_root.tres needs to be put on the child AnimationTree, otherwise there are weird control errors.
var current_moveset = 0

var wind_columns_inside: Array[WindColumn3D] = []

var combat_mode = false
var combat_relax_timer = 0

# Enums for state handedness 
enum HandState {UNARMED, ONE_HAND, TWO_HAND}
var hand_state : HandState = HandState.UNARMED

@export_group("Dress Up")
@onready var dup : DresserUpper = $DresserUpper
# SECTION Outfit
@export var garments : Array[Garment] # NOTE Resource references
@export_subgroup("Weapon (debug)") # TODO - replace with weapons in the Character class
@export var to_equip_wep : Accessory
var r_wep : Armament
var l_wep : Armament
@export var moveset_0 : MovementPackage
@export var moveset_1 : MovementPackage
# !SECTION
@export_group("Character Stats") # TODO - Replace with things in the Character class
var hurtboxes
var alive = true

# SECTION Signals 
# for leveling bar
signal actor_killed(actor_node:Actor)
signal killed_something
signal xp_get
signal item_get(item_name)
signal attack_hit(actor_hit, attack_id)
# !SECTION Signals

@onready var start_pos = global_position

func _enter_tree() -> void:
	ALL_EVER_MADE.append(self)
	$MultiplayerSynchronizer.add_visibility_filter(MgrMultiplayer.visibility_filter)
	if name.begins_with("PLAYER|"):
		var id = int(name.split("|")[1])
		set_multiplayer_authority(id)
		if id == multiplayer.get_unique_id():
			add_to_group("local_player")

func _ready():
	character = character.duplicate()
	character.reset()
	hurtboxes = find_hurtboxes_recursive(self)
	dup.connect("accessory_changed", _on_accessory_changed)
	dress_up()

	if len(movement_sets) > 0:
		compile_new_anim_tree()

	# FIXME - Workaround for offhand item not being unique
	#var narr : Array[InventoryItem] = []
	#for x in character.hand_left_slots:
	#	narr.append(x.duplicate_deep(Resource.DEEP_DUPLICATE_ALL))
	#character.hand_left_slots = narr


## Spawn or Respawn setup
func spawn():
	character.reset()
	global_position = start_pos
	character.alive = true
	animation_tree.active = true
	current_moveset = 0
	animation_tree["parameters/playback"].travel(movement_sets[current_moveset].name)
	pass 


func find_hurtboxes_recursive(node : Node):
	var returnable = []
	for child in node.get_children():
		if "hurtbox" in child.name:
			returnable.append(child)
		else:
			returnable.append_array(find_hurtboxes_recursive(child))
	return returnable

func dethrall():
	desired_move = Vector3.ZERO
	desired_turn = Vector2.ZERO

func enthrall():
	return
	#TODO - Make check player/ai authority


func _process(delta):
	action_q_process()
	if character.health_current <= 0 and character.alive == true:
		# NOTE - A special case for death seems fine
		character.alive = false
		animation_tree.active = false
		$skeleton/AnimationPlayer.play("death")
		velocity = Vector3.ZERO
		emit_signal("actor_killed", self) # NOTE: added for dungeon tools demo
		if name == "PLAYER|1":
			await get_tree().create_timer(5).timeout
			MgrTransition.level_transition(MgrTransition.current_level.resource_path, "spawn_start")
		#spawn()
		
		
	if character.alive == false:
		return
	character.stats_regen(delta)

	if len(movement_sets) > 0:
		movement_package_checks()
		movement_sets[current_moveset].move_thrall(self, delta)
	else:			
		var old_vel = velocity
		# Get the motion delta.
		velocity = ((animation_tree.get_root_motion_rotation_accumulator().inverse() * get_quaternion()) * animation_tree.get_root_motion_position() / delta) * 1

		# Add the gravity.
		if not is_on_floor():
			velocity = old_vel
		#velocity.y -= gravity * delta
		quaternion = quaternion * ((animation_tree.get_root_motion_rotation() / delta) * 10)
		# Actually move thrall


func _physics_process(_delta):
	if alive == false:
		return	
	apply_animation_params()	
	_TEMPORARY_fall_death()
	#movement_sets[current_moveset].move_thrall(self, _delta)

	move_and_slide()
	if is_multiplayer_authority():
		net_sync.rpc(desired_move, rotation, position, sprint, character.health_current, hand_state, current_moveset)
	return




func apply_animation_params():
	# NOTE - This is a hack, i'll detail how it works.
	# all animator parameters are iterated through,
	# their names are checked for certain keys.
	# Each key implies (ugh) a certain value type
	# Example, MOVE will be given the vector2 desired move
	# Others to come. It's awful, I know. 

	# SECTION Our current things, computing them once: 
	var tmd =  Vector2(( global_basis.inverse() * -desired_move).x,-( global_basis.inverse() * -desired_move).z)
	transformed_move_dir = lerp(transformed_move_dir, tmd, 0.25)
	for ani in aset_BLOCK:
		animation_tree.set(ani, 1 if "block" in action_q.keys() else 0)
	for ani in aset_MOVE:
		animation_tree.set(ani, transformed_move_dir)
	for ani in aset_TURN:			
		animation_tree.set(ani, desired_turn)
	for ani in aset_JUMP:
		animation_tree.set(ani, 1 if desired_move.y > 0.5 else 0)
	# !SECTION

func handle_movement(movement : Vector3):
	desired_move = movement

# Resets position to origin
func _TEMPORARY_fall_death():
	if global_position.y <= -20:
		global_position = Vector3(0,0.1,0)


func movement_package_checks():
	if Input.is_key_pressed(KEY_0):
		return
	if is_multiplayer_authority() == false:
		return
	# Check to see if we need to move to a new state
	var state_machine = animation_tree["parameters/playback"]
	for x in range(1, movement_sets.size()):
		if x == current_moveset:
			continue
		if movement_sets[x].transfer_situation_check(self):
			# Check if is attack moveset and currently equipped
			if movement_sets[x].pack_type() == "mvpk_combat":
				hand_state = HandState.ONE_HAND
			else:
				hand_state = HandState.UNARMED
			#	print(r_wep.moveset.name)
			#	print(movement_sets[x])
			#	if r_wep.moveset == movement_sets[x]:
			#		current_moveset = x
			#		state_machine.travel(movement_sets[current_moveset].name)
			#		return
			#else:
			current_moveset = x
			
			state_machine.travel(movement_sets[current_moveset].name)
			if is_multiplayer_authority():
				net_anim_pack_change.rpc(current_moveset)
			return
	# Check to see if we need to bail out of our current state
	if current_moveset != 0 and movement_sets[current_moveset].release_situation_check(self):
		current_moveset = 0
		state_machine.travel(movement_sets[current_moveset].name)
		if is_multiplayer_authority():
			net_anim_pack_change.rpc(current_moveset)
		return

@rpc
func net_anim_pack_change(pack : int):
	#print("Current: %d, Remote: %d" % [current_moveset, pack])
	if pack != current_moveset:
		#print("Gotta swap them bad boys!")
		current_moveset = pack
		animation_tree["parameters/playback"].travel(movement_sets[current_moveset].name)


var attackID = 0

func awful_practice_find_parent_actor(node : Node3D):
	if node is Actor:
		return node
	else:
		return awful_practice_find_parent_actor(node.get_parent())
	

func dress_up():
	if character.base_skin != null:
		dup.garment_equip(character.base_skin)
	elif character.under_skeleton != null:
		dup.garment_equip(character.under_skeleton)
	for garment in character.outfit:
		dup.garment_equip(garment)

	# NOTE TEST equip weapon as accessory... maybe? idk what is even going on here now
	# TODO Improve this
	#if len(character.hand_left_slots) > 0 and character.hand_left_slots[character.hand_l_current] != null:
	#	#character.hand_left = character.hand_left.duplicate()
	#	character.hand_left_slots[character.hand_l_current].bones[0] = "prop.L" # NOTE - This is a workaround
	#	dup.accessory_equip(character.hand_left_slots[character.hand_l_current])
	#	l_wep = dup.accessory_item(character.hand_left_slots[character.hand_l_current]).get_child(0) as Armament
	#	l_wep.equip_armament(self, true)

	right_item_inc(0)
	left_item_inc(0)
	

## Moves <move> around the item array, wraps around array length
func belt_item_inc(move : int) -> void:
	if len(character.belt_items) == 0:
		return
	character.belt_current = (character.belt_current + move) % len(character.belt_items)
## Moves <move> around the item array, wraps around array length
func right_item_inc(move : int) -> void:
	if len(character.hand_right_slots) == 0:
		return
	#unequip thing if thing 
	var accessory : Accessory = character.hand_right_slots[character.hand_r_current].held_item
	dup.accessory_unequip(accessory)
	if character.hand_r_current < 0:
		return
	character.hand_r_current = (character.hand_r_current + move) % len(character.hand_right_slots)
	#equip thing
	accessory = character.hand_right_slots[character.hand_r_current].held_item
	dup.accessory_equip(accessory, "prop.R")
	
## Moves <move> around the item array, wraps around array length
func left_item_inc(move : int) -> void:
	if len(character.hand_left_slots) == 0:
		return
	#unequip thing if thing 
	var accessory : Accessory = character.hand_left_slots[character.hand_l_current].held_item
	dup.accessory_unequip(accessory)
	if len(character.hand_left_slots) == 1 and character.hand_l_current == 0:
		character.hand_l_current = -1
		return
	else:
		character.hand_l_current = (character.hand_l_current + move) % len(character.hand_left_slots)
	#equip thing
	accessory = character.hand_left_slots[character.hand_l_current].held_item
	dup.accessory_equip(accessory,"prop.L")



## Moves <move> around the spell array, wraps around array length
func spell_inc(move : int) -> void:
	if len(character.spells_slots) == 0:
		return
	character.spell_current = (character.spell_current + move) % len(character.spells_slots)



var damage_attack_id_buffer = []
func take_damage(damage_data : Dictionary, id : int) -> Armament.AttackState:
	if !is_multiplayer_authority():
		return Armament.AttackState.PUSH
	if invulnerable:
		return Armament.AttackState.MISS # Dodge
	var returnable = Armament.AttackState.HIT
	var damage = 1
	if "physical" in damage_data.keys():
		damage = damage_data["physical"]
	if id not in damage_attack_id_buffer:
		damage_attack_id_buffer.append(id)
		if block && character.stamina_current > 0:
			character.stamina_current -= damage
			enque_action("blocked_attack")
			returnable = Armament.AttackState.BLOCKED
			if character.stamina_current <= 0:
				character.poise_current += character.stamina_current
		else:
			character.health_current -= damage
			if "poise" in damage_data.keys():
				character.poise_current -= damage_data["poise"]
		if character.poise_current <= 0:
			if "poise_break" not in action_q.keys():
				enque_action("poise_break", 1500) # stop double falling
			character.poise_regen_timer = 0.5
		else:
			character.poise_regen_timer = character.poise_regen_delay
	else:
		returnable = Armament.AttackState.MISS
	#if health_current <= 0:
	#	alive = false
	return returnable

func block_attack(id : int):
	if id not in damage_attack_id_buffer:
		damage_attack_id_buffer.append(id)
	


func compile_new_anim_tree():
	var master_tree = AnimationNodeStateMachine.new()
	master_tree.state_machine_type = AnimationNodeStateMachine.STATE_MACHINE_TYPE_NESTED
	var counter = 0
	## FIXME - Hard coded adding in movement sets from weapons
	for arm in character.hand_right_slots:
		if arm.moveset not in movement_sets:
			movement_sets.append(arm.moveset)
	for mvpk in movement_sets:
		master_tree.add_node(mvpk.name, mvpk.anim_tree.duplicate(true), Vector2(0, counter))

	var masterstate_transition = AnimationNodeStateMachineTransition.new()
	masterstate_transition.advance_mode = AnimationNodeStateMachineTransition.ADVANCE_MODE_ENABLED
	masterstate_transition.xfade_time = 0.1
	for mvpk in movement_sets:
		for othermove in movement_sets:
			if mvpk == othermove:
				continue
			master_tree.add_transition(mvpk.name, othermove.name, masterstate_transition.duplicate())

	animation_tree.tree_root = master_tree # neat. What? No transitions or anything
	var state_machine = animation_tree["parameters/playback"]
	#state_machine.travel()
	state_machine.start(movement_sets[0].name)

	# Initialize default values
	# NOTE - This is a hack.
	# This iterates through paramiter names, then extracts default values from them, and applies them
	# This is a hack, because little things like speeds or blends are saved on the animated thing,
	# Not in the animation tree 
	var param_names = animation_tree.get_property_list() #._get_parameter_list()
	for item in param_names: # not actually param names now, still trying to get a list'
		if item.name.begins_with("parameters/"):
			if item.name.contains("SET_F["):
				var workstring = item.name.substr(item.name.find('[')+1)
				workstring = workstring.replace("-", ".") # NOTE - Another hack, param names cant have dots
				workstring = workstring.substr(0, workstring.find(']'))
				animation_tree.set(item.name, workstring.to_float())
			elif item.name.contains("MOVE") and item.name.contains("blend_position"):
				aset_MOVE.append(item.name)
			elif item.name.contains("TURN") and (item.name.contains("blend_position") or item.name.contains("add_amount")):				
				aset_TURN.append(item.name)
			elif item.name.contains("BLOCK"): # and item.name.contains("blend_position"):
				aset_BLOCK.append(item.name)
			elif item.name.contains("JUMP"): # and item.name.contains("blend_position"):
				aset_JUMP.append(item.name)
		# This is a fix for the T-posing walk/sprint problem. 
		# There is some error in nexted AnimationStateMachines automatically playing
		# So here we manually call the problem child and start it.
		# TODO - Make more generic, or find the cause of the error in starting
		if item.name.contains("WalkSprint/playback"):
				animation_tree.get(item.name).travel("Start")
	animation_tree.active = true # set here to stop minor bone movement from showing up over and over again in git

	#animation_tree.connect("animation_finished", cb)
func cb(msg : String):
	print("CB MSG: " + msg)

# SECTION Action Queue and buffer system
# NOTE - Action queue system. Things are put on and have a short 
# buffer time after which they are knocked off. I am not sure if this
# is a good approach, but I'm trying it out
const ACTION_Q_BUFFER_TIME = 33 #milliseconds, about 2 frames at 60
var action_q : Dictionary[String, int] = {}

func enque_action(action : String, duration : int = ACTION_Q_BUFFER_TIME):
	action_q[action] = Time.get_ticks_msec() + (duration)
	if "attack" in action:
		combat_mode = true
		combat_relax_timer = 5.0

# Remove items that have outlived their usefulness
func action_q_process():
	var curTick = Time.get_ticks_msec()
	for key in action_q.keys():
		if action_q[key] <= curTick:
			action_q.erase(key)

func action_q_check(action : String, consume=false) -> bool: 
	# NOTE - Warning, string comparisons. Not great. 
	if action_q.size() == 0:
		return false
	if action in action_q.keys():
		if is_multiplayer_authority():
			_action_q_net.rpc(action, (action_q[action]) - Time.get_ticks_msec())
		if consume == true:
			action_q.erase(action)
		return true
	return false

@rpc
func _action_q_net(act : String, _remaining_duration : int):
	enque_action(act)
# !SECTION - End action_q system

# SECTION - Animation assistance functions 
# To be called by animation keyframes 

# NOTE - This is somewhat undesireable. 
#		Movement code was supposed to be in the movement package
#		 Additionally, lock_targ_pos was added in as a HACK
func anim_track_look():
	if lock_targ_pos != Vector3.ZERO:
		var evened_look_pos = lock_targ_pos
		evened_look_pos.y = global_position.y
		look_at(global_position + (global_position - evened_look_pos))
	elif desired_move.length_squared() > 0.05:# != Vector3.ZERO:
		look_at(global_position - desired_move)
	#look_at(global_position + (global_position - lock_targ_pos))

func anim_track_move():
	if global_position == desired_move:
		return
	if desired_move.length_squared() > 0.05:
		look_at(global_position - desired_move)
	#look_at(global_position + (global_position - lock_targ_pos))
	

## Activates an item in a hand for melee damage
## <hands> is faux enum, null or 0 is right, 1 is left, 2 is both.
func anim_hurtbox_activate(stanima_cost : float, dvalues : Dictionary, hands : int):
	character.stamina_current -= stanima_cost
	character.stamina_regen_timer = character.stamina_regen_delay
	if hands == null or hands == 0:
		r_wep.activate_strike(character.get_current_hand_r().dvalues)
	elif hands == 1:
		l_wep.activate_strike(character.get_current_hand_l().dvalues)
	elif hands == 2:
		r_wep.activate_strike(character.get_current_hand_r().dvalues)
		l_wep.activate_strike(character.get_current_hand_l().dvalues)

## Inital pulling out of item, hide weapons
func anim_item_start():
	anim_hide_weapons()
	var current_item = character.get_current_belt()
	if "prop.R" in current_item.extra_resources.keys():
		dup.accessory_equip(current_item.extra_resources["prop.R"], "prop.R")
	#var prop = character.get_current_belt().drop_item.instantiate()
	#prop.name = "prop"
	#r_wep.get_parent().add_child(prop)
	pass 

## Actual use and consumption of item, apply effect
func anim_item_use():

	pass 

## Turn off temporary item, return to weapons
func anim_item_end():

	var current_item = character.get_current_belt()
	if "prop.R" in current_item.extra_resources.keys():
		dup.accessory_unequip(current_item.extra_resources["prop.R"], "prop.R")
	anim_show_weapons()
	pass

func anim_hide_weapons():
	if is_instance_valid(l_wep):
		l_wep.visible = false
	if is_instance_valid(r_wep):
		r_wep.visible = false

func anim_show_weapons():	
	if is_instance_valid(l_wep):
		l_wep.visible = true
	if is_instance_valid(r_wep):
		r_wep.visible = true

func invulnerability_time(time : float):
	var timer = get_tree().create_timer(time)
	invulnerable = true
	await timer.timeout
	invulnerable = false

## Plays an animation once if it is present. 
## This temporarily disables the animation tree,
## then resumes it when this animation finishes
func animation_one_shot(anim_name : String, hide_weapons = true, play_backwards = false):
	var a_anim : AnimationPlayer = animation_tree.get_node(animation_tree.anim_player) 
	if a_anim.has_animation(anim_name) == false:
		return
	if hide_weapons:
		anim_hide_weapons()
	animation_tree.active = false
	if play_backwards:
		a_anim.play_backwards(anim_name) 
	else:
		a_anim.play(anim_name) 	
	await a_anim.animation_finished
	if hide_weapons:
		anim_show_weapons()
	animation_tree.active = true
# !SECTION - Animation helper functions

func multiplayer_despawn():
	var a_anim : AnimationPlayer = animation_tree.get_node(animation_tree.anim_player) 
	animation_tree.active = false
	a_anim.play("spawns_and_deaths/despawn_multiplayer_disconnect") 	
	await a_anim.animation_finished 
	visible = false
	queue_free()

## Plays spawning animation and makes node visible
## Player calls it I guess
func multiplayer_spawn():
	#print("Spawn point: " + MgrTransition.target_spawn_point)
	#print("Spawn logic: " + str(get_tree().current_scene))
	var spawnpoint : Spawnpoint3D = (get_tree().current_scene as Level).find_child(MgrTransition.target_spawn_point)
	#print("Spawn subtp: " + str(spawnpoint.subtype))
	rpc_mp_spawn.rpc(MgrPlayerSocket.player_type, spawnpoint.subtype) 

@rpc("call_local")
func rpc_mp_spawn(player_type : String, anim : int):
	visible = true	
	if anim == 0:
		animation_one_shot("spawns_and_deaths/spawn_default")
	if anim == 1: 
		if player_type == "ally":
			animation_one_shot("spawns_and_deaths/spawn_multiplayer_priaseTheSun")
		else:
			animation_one_shot("spawns_and_deaths/spawn_multiplayer_invasion")
	elif anim == 2:
		animation_one_shot("spawns_and_deaths/spawn_walk_in")
	if player_type == "main":
		add_to_group("allies")
	elif player_type == "ally":
		dup.set_material_overlay(MgrPlayerSocket.ally_material)
		add_to_group("allies")
	elif player_type == "enemy":
		dup.set_material_overlay(MgrPlayerSocket.enemy_material)
		add_to_group("enemies")
		if is_multiplayer_authority() == false:
			get_node("actor_nametag").set_nametag_visibility(ActorNametag.VisState.CHANGE)
	



@rpc("unreliable")
func net_sync(d_move : Vector3, c_rotation : Vector3, c_position : Vector3, c_sprint : bool, c_health, c_hand_state : int, c_mvpk):
	if is_multiplayer_authority():
		return
	#print(str(multiplayer.get_unique_id()) + "| Update for " + str(multiplayer.get_remote_sender_id()) +" for " + name)
	desired_move = d_move
	rotation = c_rotation
	position = c_position
	sprint = c_sprint
	character.health_current = c_health
	hand_state = c_hand_state as HandState
	if c_mvpk != current_moveset:
		current_moveset = c_mvpk
		var state_machine = animation_tree["parameters/playback"]
		state_machine.travel(movement_sets[current_moveset].name)


func _on_accessory_changed():
	# Accessories changed, this is how weapons are handled n shit, so we need to chiggidy check that shit. 
	
	var temp_r = dup.accessory_on_bone("prop.R")
	if temp_r and temp_r.get_child(0) is Armament:
		r_wep = temp_r.get_child(0) as Armament
		r_wep.equip_armament(self, false)
	else:
		r_wep = null
	var temp_l = dup.accessory_on_bone("prop.L")
	if temp_l and temp_l.get_child(0) is Armament:
		l_wep = temp_l.get_child(0) as Armament
		l_wep.equip_armament(self, true)
	else:
		l_wep = null
